plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group "ai.djl.jsr381"
boolean isRelease = project.hasProperty("release") || project.hasProperty("staging")
version = "0.3.0" + (isRelease ? "" : "-SNAPSHOT")

dependencies {
    api "javax.visrec:visrec-api:1.0-SNAPSHOT"
    api "ai.djl:api:0.3.0"
    api "ai.djl:basicdataset:0.3.0"
    api "ai.djl:model-zoo:0.3.0"
    api "ai.djl.mxnet:mxnet-model-zoo:0.3.0"
    api 'ai.djl.mxnet:mxnet-engine:0.3.0'
    api 'ai.djl.mxnet:mxnet-native-auto:1.6.0'
    api "org.apache.commons:commons-csv:1.7"

    testImplementation "org.slf4j:slf4j-simple:1.7.30"
    testImplementation("org.testng:testng:6.14.3") {
        exclude group: "junit", module: "junit"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    // Use TestNG for unit tests
    useTestNG()
}

apply from: file("${rootProject.projectDir}/tools/gradle/formatter.gradle")

project.tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    required(project.hasProperty("staging") || project.hasProperty("snapshot"))
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    if (gradle.startParameter.taskNames.contains("publish")) {
        throw new GradleException("JDK 1.8 is required to run publish task.")
    }
    return
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifacts = [jar, javadocJar, sourcesJar]
            pom {
                name = "Deep Java Library implementation of JSR-381"
                description = "Deep Java Library implementation of JSR-381"
                url = "http://www.djl.ai/"

                packaging = "jar"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                scm {
                    connection = "scm:git:git@github.com:awslabs/djl.git"
                    developerConnection = "scm:git:git@github.com:awslabs/djl.git"
                    url = "https://github.com/awslabs/djl"
                    tag = "HEAD"
                }

                developers {
                    developer {
                        name = "DJL.AI Team"
                        email = "djl-dev@amazon.com"
                        organization = "Amazon AI"
                        organizationUrl = "https://amazon.com"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            if (project.hasProperty("snapshot")) {
                name = "snapshot"
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = findProperty("ossrhUsername")
                    password = findProperty("ossrhPassword")
                }
            } else if (project.hasProperty("staging")) {
                name = "staging"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("ossrhUsername")
                    password = findProperty("ossrhPassword")
                }
            } else {
                name = "local"
                url = "build/repo"
            }
        }
    }
}
